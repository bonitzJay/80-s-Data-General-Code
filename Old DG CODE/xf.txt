IDENTIFICATION DIVISION.
PROGRAM-ID. XF.
AUTHOR. JAY ANDERSON.
**********************************************************
*    SCAN DIRECTORY STRUCTURE LOOKING FOR JUNK FILES SUPPLIED
*    AS TEMPLATES FROM THE COMMAND LINE.  ALLOW THE SYSTEM
*    TO DO ALL THE WORK SO I'M NOT TOO EFFICIENT.
*********
*    THIS PROGRAM ADDRESSES MOST OF WHAT CONSTITUTES A DISK
*    FILING SYSTEM ANALYSIS.  IT IS BASICALLY A FILESTATUS
*    REPORT IN COBOL.  ITS STRONGEST POINT IS THE DUPLICATE
*    FILES REPORT AND THE ABILITY TO LOOK FOR AND OPTIONALLY
*    DELETE JUNK FILES.  IT PRESENTS FILE SYSTEM INFORMATION
*    IN AN EASY TO READ FORMAT SO IT'S EASY TO SPOT POTENTIAL
*    PROBLEM AREAS.  STRUCTURE COUNTS AND FILECOUNTS POINT OUT
*    POSSIBLE HASH FRAME PROBLEMS.
*********
*    EXAMPLE:
*    X XF/S &
*      +.BRK/D &
*      ?.+.BRK &
*      ?.CBL.+
*  SWITCHES-------
*   XF/S ---SINCE SWITCH -REPORT DATES AS DAYS SINCE
*      TEMPLATE/D  ---DELETES ALL FILES THAT MATCH THE TEMPLATE.
***********************************************************
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SPECIAL-NAMES.
SOURCE-COMPUTER. ECLIPSE.
OBJECT-COMPUTER. ECLIPSE.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
SELECT OUTPRINT ASSIGN TO PRINTER OUTPRINTFILE
	RESERVE 1 AREA ORGANIZATION SEQUENTIAL
	ACCESS SEQUENTIAL.

SELECT FORSORT ASSIGN TO DISK  FORSORTFILE
	RESERVE 1 AREA ORGANIZATION SEQUENTIAL
	ACCESS SEQUENTIAL.

SELECT OUTSORT ASSIGN TO  OUTSORTFILE
	RESERVE 1 AREA ORGANIZATION SEQUENTIAL
	ACCESS SEQUENTIAL.

SELECT PATHFILE ASSIGN TO PATHWORKFILE
        ORGANIZATION IS RELATIVE
           ACCESS IS DYNAMIC
           RELATIVE KEY IS XREL1.

DATA DIVISION.
FILE SECTION.
FD PATHFILE
        LABEL RECORD OMITTED.
01 PATHBUFFER.
        05  FILLER	PIC X(512).
FD FORSORT
   BLOCK CONTAINS 8192 CHARACTERS.
01 FORSORTREC.
	05  OS-FILE	PIC X(32).
	05  OS-STRUCT	PIC 9(6).
	05  OS-TYPE	PIC XXX.
SD OUTSORT
   BLOCK CONTAINS 8192 CHARACTERS.
01 OUTSORTREC.
	05  OSFILE	PIC X(32).
	05  OSSTRUCT	PIC 9(6).
	05  OSLNK	PIC XXX.
FD OUTPRINT
   BLOCK CONTAINS 8192 CHARACTERS.
01 PLINE.
	05  FILLER		PIC X(132).
WORKING-STORAGE SECTION.
77  SP				PIC 9(9) COMP.
77  XREL1			PIC 9(9) COMP.
77  PATHWORKFILE		PIC X(256).
77  OUTSORTFILE			PIC X(256).
77  FORSORTFILE			PIC X(256).
77  WORKDIR			PIC X(128) VALUE ' '.
77  STARTPATH			PIC X(128) VALUE ' '.
77  TODAYSFDAY			PIC 9(9) COMP.
77  SAVEPLINE			PIC X(132).
77  LINE-LIMIT 			PIC 9(5) COMP VALUE 60.
77  LINE-CNT			PIC 9(5) COMP.
77  PAGE-CNT			PIC 9(5) COMP.
77  SINCE-SW			PIC X.
	88  SINCE    		VALUE '1'.
77  JUNKFILETOT			PIC 9(9) COMP.
77  JUNKDELFILETOT		PIC 9(9) COMP.
77  JUNKDELFILETOTBYTES		PIC 9(9) COMP.
77  FIRSTPRINT-SW		PIC X.
	88  FIRSTPRINT		VALUE '1'.
77  WSAGE			PIC S9(9) COMP.
77  ONEISROOT-SW		PIC X.
	88  ONEISROOT		VALUE '1'.
77  OUTPRINTFILE		PIC X(32).
01  STR1			PIC X(120).
01  STR2			PIC X(120).
01  STR3			PIC X(120).
01  HOLDFORSORTREC		PIC X(41).
01  HOLDOS-FILE			PIC X(32).
01  OUTOS.
	05  FILLER		PIC X(20) VALUE SPACE.
	05 OUTOS-FILE		PIC X(32).
	05 OUTOS-STRUCT		PIC Z(6).
01  LOOKAHEADBUF.
	05  LA-FILE		PIC X(32).
	05  FILLER   		PIC 9(6).
	05  FILLER		PIC XXX.
01  PRINT-ON-SW			PIC X.
	88  PRINT-ON		VALUE '1'.
01  FILLER.
	05  TMPPLACE.
		10  FILLER	PIC X.
		10  TEMP1X	PIC X.
	05  TEMP1 REDEFINES TMPPLACE PIC 9(4) COMP.
	05  TEMP2		PIC 9(4) COMP.
01  BIGTEMP			PIC S9(9) COMP.
01  BIGPARM.
	05  CALLSTR		PIC X(18).
	05  BIGLINE OCCURS 7    PIC X(128).
	05  FILLER		PIC X(62).
01  BIGI			PIC 9(4) COMP.
01 FILLER.
	05  SSTSWORKX.
		10  FILLER	PIC XX.
		10  SSTSWORKLOWX PIC XX.
	05 SSTSWORK REDEFINES SSTSWORKX PIC S9(9) COMP.
01  HIGHEST-TEMP		PIC 9(4) COMP.
01  TEMPLATETABLE.
    05 TEMPTABLEENTRY	OCCURS 32.
	10  ATEMPLATE		 PIC X(32).
	10  OKTODEL-SW			PIC X.
		88  OKTODEL		VALUE '1'.
01  GETME-WS.
	05  GMES		PIC S9(4) COMP VALUE ZERO.
	05  GCMD		PIC S9(4) COMP VALUE 1.
	05  GCNT		PIC S9(4) COMP VALUE 2.
	05  GARG		PIC S9(4) COMP VALUE 3.
	05  GTSW		PIC S9(4) COMP VALUE 4.
	05  GSWS		PIC S9(4) COMP VALUE 5.
****************************************************************
* USE WHO.WS WITH SUBROUTINE WHO TO OBTAIN PID CONSOLE OR
* STREAM NAME AND USERNAME.
****************************************************************
01  WHO-PID		        PIC 999.
01  WHO-CON.
	05 WHO-CON1		PIC X.
	    88  RUNNING-CONSOLE	VALUE 'C' 'V'.
	    88  RUNNING-BATCH  	VALUE 'S'.
	05 FILLER PIC X(31) VALUE SPACES.
01  WHO-USER                    PIC X(32) VALUE SPACES.
01  FORMFEEDER.
	05  FILLER		PIC 99 COMP VALUE 12.
01  NEWLINER.
	05  FILLER		PIC 99 COMP VALUE 10.
01  DELETED-MESS.
	05  FILLER		PIC X(56) VALUE
	 'I deleted the following files for template '.
	05  OUTTEMPLATE1	PIC X(32).
01  WOULDHAVE-MESS.
	05  FILLER		PIC X(56) VALUE
	 'I would have deleted the following files for template '.
	05  OUTTEMPLATE2	PIC X(32).
01  DIRLINE1.
	05  OUTFILEN2		PIC X(15) VALUE
	'STRUCTURE #'.
	05  ODIRCNT		PIC ZZZZZ.
	05  FILLER		PIC X(16) VALUE
	 ' FILE COUNT....'.
	05  OFCNT		PIC ZZZZZZZZZ.
	05  FILLER		PIC X(22) VALUE
	 ' FILES IN STRUCTURE.'.
	05  OSCNT		PIC ZZZZZZZZZ.
	05  FILLER		PIC X(12)  VALUE ' HASHFRAME '.
	05  OHASH		PIC ZZZZ.
01  DIRLINE2.
	05  FILLER		PIC X(6) VALUE 'SPACE'.
	05  FILLER		PIC X(6) VALUE ' MAX '.
	05  OMAX		PIC ZZZZZZZZZ.
	05  FILLER		PIC X(6) VALUE ' CUR '.
	05  OCUR		PIC ZZZZZZZZZ.
	05  FILLER		PIC X(6) VALUE ' REM '.
	05  OREM		PIC ZZZZZZZZZ.
	05  FILLER		PIC X(15) VALUE ' PERCENT FULL'.
	05  OPER		PIC ZZ.
01  OUTDETAIL.
    03  FILLER.
	05  OUTFILEN		PIC X(50).
	05  FILLER		PIC X VALUE SPACE.
	05  FNL			PIC X(3).
	05  FILLER		PIC X VALUE SPACE.
	05  OUTTYPE		PIC 999.
	05  OUTTYPEX REDEFINES OUTTYPE.
		10  OT1		PIC X.
		10  OT2		PIC X.
		10  OT3		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OUTCREATED		PIC Z9/99/99.
	05  SINCECREATED REDEFINES OUTCREATED
				PIC ZZZZZZZ9.
	05  FILLER		PIC X VALUE SPACE.
     03 OVERLAYREC.
	05  OUTMODIFIED		PIC Z9/99/99.
	05  SINCEMODIFIED REDEFINES OUTMODIFIED
				PIC ZZZZZZZ9.
	05  FILLER		PIC X VALUE SPACE.
	05  OUTACCESSED		PIC Z9/99/99.
	05  SINCEACCESSED REDEFINES OUTACCESSED
				PIC ZZZZZZZ9.
	05  FILLER		PIC X VALUE SPACE.
	05  OUTSIZE		PIC ZZZZZZZZ9.
	05  FILLER		PIC X VALUE SPACE.
	05  OSIDX		PIC 9.
	05  OSLSH		PIC X.
	05  OMIL		PIC 9.
	05  FILLER		PIC X VALUE SPACE.
	05  OUTELEMENT		PIC ZZZZZZZZZ.
	05  OUTELEX REDEFINES OUTELEMENT PIC X(9).
	05  FILLER		PIC X VALUE SPACE.
	05  OUTOPEN		PIC ZZ9.
	05  FILLER		PIC XX VALUE SPACE.
	05  OSHARE		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OMOD		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OPERM		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  ODOC		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OUDA		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OURA		PIC X.
	05  FILLER		PIC X VALUE SPACE.
	05  OUEA		PIC X.
01  HEADTIP.
	05  FILLER		PIC X(50) VALUE SPACE.
	05  OUTSYSID		PIC X(52).
	05  FILLER		PIC X(17) VALUE 'RUN DATE'.
	05  OUTDATE		PIC Z9/99/99.
01  HEADDUP.
	05  FILLER		PIC X(50) VALUE SPACE.
	05  FILLER  		PIC X(52) VALUE
	 'DUPLICATE FILES REPORT'.
01  HEADERDUP.
	05  FILLER		PIC X(35) VALUE
	 '  FILENAME'.
	05  FILLER		PIC X(30) VALUE
	 ' STRUCTURE'.
01  HEADTOP.
    03  FILLER.
	05  FILLER  		PIC X(50) VALUE '      FILENAME'.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER		PIC X(3) VALUE 'FNL'.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER 		PIC X(4) VALUE 'TYPE'.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER    	PIC X(8)  VALUE "CREATED".
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER     		PIC X(8) VALUE    'MODIFIED'.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER     		PIC X(8) VALUE 'ACCESSED'.
	05  FILLER 		PIC X(9) VALUE '   SIZE'.
	05  FILLER		PIC X(5) VALUE 'INDEX'.
	05  FILLER		PIC XX VALUE SPACE.
	05  FILLER    		PIC X(8) VALUE 'ELEMENT'.
	05  FILLER  		PIC X(4) VALUE 'OPEN'.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER 		PIC X(13) VALUE ALL '-'.
01  HEADX1.
    03  FILLER.
	05  FILLER  		PIC X(50) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER		PIC X(3) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER 		PIC X(4) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER    		PIC X(8) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER     		PIC X(8) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER 		PIC X(8) VALUE SPACE.
	05  FILLER 		PIC X(9) VALUE SPACE.
	05  FILLER		PIC X(5) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER    		PIC X(9) VALUE SPACE.
	05  FILLER  		PIC X(4) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  PFLAGS 		PIC X(13) VALUE SPACE.
01  FLAGD1  PIC X(13) VALUE
		'S M P D U U U'.
01  FLAGD2  PIC X(13) VALUE
		'H O R O D R E'.
01  FLAGD3  PIC X(13) VALUE
		'R D M C A A A'.
01 FMM				PIC 9(9) COMP.
01 FDD				PIC 9(9) COMP.
01 FYY				PIC 9(9) COMP.
01 FRETURN			PIC 9(9) COMP.

01  PP				PIC 9(4) COMP.
01  STATHEAD.
	05  FILLER		PIC X(20) VALUE 'JUNK FILES'.
	05  FILLER		PIC X(24) VALUE 'JUNK FILES DELETED'.
	05  FILLER		PIC X(30) VALUE 'TOTAL BYTES RECOVERED'.
	05  FILLER		PIC X(25) VALUE '# STRUCTURES SCANNED'.
01  STATLINE.
	05  OUTJUNKFILETOT	PIC ZZZ,ZZZ,ZZ9.
	05  FILLER		PIC X(9) VALUE SPACE.
	05  OUTJUNKDELFILETOT	PIC ZZZ,ZZZ,ZZ9.
	05  FILLER		PIC X(11) VALUE SPACE.
	05  OUTJUNKDELFILETOTBYTES PIC ZZZ,ZZZ,ZZ9.
	05  FILLER		PIC X(20) VALUE SPACE.
	05  OUTDIRECTORYCOUNTER PIC ZZZ,ZZZ,ZZ9.
01  DET-HEAD.
	05  FILLER		PIC X(33) VALUE 'FILE'.
	05  FILLER		PIC X(6) VALUE '     '.
	05  FILLER		PIC X(7) VALUE 'ACTION'.
01  DET-LINE.
	05  DET-FILE		PIC X(32).
	05  FILLER		PIC X VALUE SPACE.
	05  FILLER 		PIC X(5) VALUE SPACE.
	05  FILLER		PIC X VALUE SPACE.
	05  DET-ACTION		PIC X(7).

01  NO-ARG			PIC 9(9) COMP VALUE 228.
01  G-ARGUMENT-NO	PIC S9(4) COMP VALUE ZERO.
01  G-SWITCH		PIC X(32) VALUE SPACES.
01  G-BUFFER		PIC X(200) VALUE SPACES.
01  G-ERRWRD		PIC S9(9) COMP VALUE ZERO.
01  G-AC0		PIC S9(9) COMP VALUE ZERO.
01  G-AC1		PIC S9(9) COMP VALUE ZERO.
01  ERRWRD		PIC S9(9) COMP.
01  ERRMSG.
	05  FILLER	PIC X(5).
		88  NO-ERROR VALUE '     '.
	05  FILLER	PIC X(75).
01 FINISHED-SW		PIC X.
	88 FINISHED VALUE '1'.
77  DIS-BUF	PIC X(32).
01  D-DUM	PIC X.
01  CTEMP	PIC X(64).
01 CDIR		PIC X(128) VALUE '='.
01 ERR-MSG.
	05  ERR-MSG3	PIC XXX.
	05  FILLER	PIC X(77).
01 RBUF	PIC X(64).
01 ESCAPE-CODE	PIC 99.
	88  E-ESC	VALUE 01.
01  FILETYPES1.
	05  FILETYPES.
		10 FILLER	PIC X(4) VALUE '<000>LNK'.
		10 FILLER	PIC X(4) VALUE '<001>SDF'.
		10 FILLER	PIC X(4) VALUE '<002>MTF'.
		10 FILLER	PIC X(4) VALUE '<003>GFN'.
		10 FILLER	PIC X(4) VALUE '<012>DIR'.
		10 FILLER	PIC X(4) VALUE '<013>LDU'.
		10 FILLER	PIC X(4) VALUE '<014>CPD'.
		10 FILLER	PIC X(4) VALUE '<015>MTV'.
		10 FILLER	PIC X(4) VALUE '<024>DKU'.
		10 FILLER	PIC X(4) VALUE '<025>MCU'.
		10 FILLER	PIC X(4) VALUE '<026>MTU'.
		10 FILLER	PIC X(4) VALUE '<027>LPU'.
		10 FILLER	PIC X(4) VALUE '<030>LPD'.
		10 FILLER	PIC X(4) VALUE '<031>LPE'.
		10 FILLER	PIC X(4) VALUE '<036>IPC'.
		10 FILLER	PIC X(4) VALUE '<040>SPR'.
		10 FILLER	PIC X(4) VALUE '<041>QUE'.
		10 FILLER	PIC X(4) VALUE '<043>GLM'.
		10 FILLER	PIC X(4) VALUE '<044>TRA'.
		10 FILLER	PIC X(4) VALUE '<045>CRA'.
		10 FILLER	PIC X(4) VALUE '<052>TPA'.
		10 FILLER	PIC X(4) VALUE '<053>PLA'.
		10 FILLER	PIC X(4) VALUE '<054>LPA'.
		10 FILLER	PIC X(4) VALUE '<055>LPC'.
		10 FILLER	PIC X(4) VALUE '<061>CON'.
		10 FILLER	PIC X(4) VALUE '<074>SYN'.
		10 FILLER	PIC X(4) VALUE '<100>UDF'.
		10 FILLER	PIC X(4) VALUE '<101>PRG'.
		10 FILLER	PIC X(4) VALUE '<102>UPF'.
		10 FILLER	PIC X(4) VALUE '<103>STF'.
		10 FILLER	PIC X(4) VALUE '<104>TXT'.
		10 FILLER	PIC X(4) VALUE '<105>LOG'.
		10 FILLER	PIC X(4) VALUE '<112>PRV'.
		10 FILLER	PIC X(4) VALUE '<113>WRD'.
		10 FILLER	PIC X(4) VALUE '<114>AFI'.
		10 FILLER	PIC X(4) VALUE '<115>AWS'.
		10 FILLER	PIC X(4) VALUE '<116>BCI'.
		10 FILLER	PIC X(4) VALUE '<117>DCF'.
		10 FILLER	PIC X(4) VALUE '<120>LCF'.
		10 FILLER	PIC X(4) VALUE '<121>LUG'.
		10 FILLER	PIC X(4) VALUE '<127>UNX'.
		10 FILLER	PIC X(4) VALUE '<130>BBS'.
		10 FILLER	PIC X(4) VALUE '<131>VLF'.
		10 FILLER	PIC X(4) VALUE '<132>DBF'.
		10 FILLER	PIC X(4) VALUE '<133>GKM'.
		10 FILLER	PIC X(4) VALUE '<134>VDM'.
		10 FILLER	PIC X(4) VALUE '<135>NAP'.
		10 FILLER	PIC X(4) VALUE '<136>TRV'.
		10 FILLER	PIC X(4) VALUE '<137>SPD'.
		10 FILLER	PIC X(4) VALUE '<140>QRY'.
		10 FILLER	PIC X(4) VALUE '<141>DTB'.
		10 FILLER	PIC X(4) VALUE '<142>FMT'.
		10 FILLER	PIC X(4) VALUE '<143>WPT'.
		10 FILLER	PIC X(4) VALUE '<144>DIF'.
		10 FILLER	PIC X(4) VALUE '<145>VIF'.
		10 FILLER	PIC X(4) VALUE '<146>IMG'.
		10 FILLER	PIC X(4) VALUE '<147>PRF'.
		10 FILLER	PIC X(4) VALUE '<150>PIP'.
		10 FILLER	PIC X(4) VALUE '<151>TTX'.
	05 FILETYPE REDEFINES FILETYPES OCCURS 59 INDEXED BY TNDX.
		10 FT		PIC X.
		10 FTNAME	PIC XXX.
01  FILLER.
	05  AWORKDATE.
		10  WMM	PIC 99.
		10  WDD	PIC 99.
		10  WYY	PIC 99.
	05  WORKERDATE REDEFINES AWORKDATE PIC 9(6).
01 WS-DATE.
	05  YY		PIC 99.
	05  MM		PIC 99.
	05  DD		PIC 99.
01  STO-DAY		PIC 9(9) COMP.
01  PERMON		PIC 9(9) COMP VALUE 212.
01  F-FILE		PIC X(128).
01  F-TYPE		PIC S9(4) COMP.
01  F-DATE.
	05  F-DATE-YY	PIC 99.
	05  F-DATE-MM	PIC 99.
	05  F-DATE-DD	PIC 99.
01  F-TIME		PIC 9(6).
01  F-SIZE		PIC S9(9) COMP.

*******END DEL WS
*  FSTAT PARAMETER PACKET -----OTHER FILE TYPES ------
01  DIRECTORYCOUNTER		PIC 99999.
01  ER-MES			PIC X(80).
01  WORK1			PIC X(35).
01  FSTAT-PACK.
*0
	05  SRECF		PIC S9(2) COMP VALUE ZERO.
	05  STYP		PIC S9(2) COMP VALUE ZERO.
	05  STYPX REDEFINES STYP PIC X.
*1
	05  STIM		PIC S9(4) COMP VALUE -1.
*2
	05  SACP		PIC S9(4) COMP VALUE -1.
*3
	05  SCPS		PIC S9(4) COMP VALUE ZERO.
*4
	05  SLAU		PIC S9(4) COMP VALUE ZERO.
*
	05  SDEH		PIC S9(9) COMP VALUE ZERO.
	05  SMSH REDEFINES SDEH	PIC S9(9) COMP.
	05  SMIL		PIC S9(4) COMP VALUE ZERO.
*
	05  STCH		PIC S9(4) COMP VALUE ZERO.
	05  STCL		PIC S9(4) COMP VALUE ZERO.
*
	05  STAH		PIC S9(4) COMP VALUE ZERO.
	05  STAL		PIC S9(4) COMP VALUE ZERO.
*
	05  STMH		PIC S9(4) COMP VALUE ZERO.
	05  STML		PIC S9(4) COMP VALUE ZERO.
*
	05  SSTS		PIC S9(4) COMP VALUE ZERO.
	05  SSTSX REDEFINES SSTS PIC X(2).
	05  SEFW		PIC S9(4) COMP VALUE ZERO.
	05  SEFL		PIC S9(4) COMP.
*
	05  SEFM		PIC S9(9) COMP.
*
	05  SFAH		PIC S9(4) COMP VALUE ZERO.
	05  SFAL		PIC S9(4) COMP VALUE ZERO.
*
	05  SIDX		PIC S9(4) COMP VALUE ZERO.
*
	05  SOPN		PIC S9(4) COMP VALUE ZERO.
	05  SCHS		PIC 9(9) COMP.
*
	05  FILLER		PIC S9(4) COMP VALUE ZERO.
*
	05  FILLER		PIC S9(4) COMP VALUE ZERO.
***
*BUFFER
	05  FILLER		PIC X(20).
01  FILLER.
	05  A1BIT	PIC S9(9) COMP.
	05  DIDLER REDEFINES A1BIT.
		10 DIDB1	PIC S99 COMP.
		10 DIDB2	PIC X.
		10 DIDB3	PIC X.
		10 DIDB4	PIC X.
01  SYSTEMID			PIC X(32).
01  A-DIR		PIC S9(4) COMP VALUE 10.
01  A-CPD		PIC S9(4) COMP VALUE 12.
01  A-DISK		PIC S9(4) COMP VALUE 11.
01  COB-GSID		PIC S9(4) COMP VALUE 117.
01  COB-DELETE		PIC S9(4) COMP VALUE 1.
01  COB-GLINK 		PIC S9(4) COMP VALUE 84.
01  COB-SUPER			PIC S9(4)  COMP VALUE 75.
01  COB-GSTAT			PIC S9(4)  COMP VALUE 63.
01 TEMPLATE		PIC X(32).
01  DELTEMPLATE		PIC X(64).
01  KILLBUFFER		PIC X(512).
01  AC0 		PIC S9(9) COMP.
01  AC1			PIC S9(9) COMP.
01  AC2			PIC S9(9) COMP.
01  ERR-RET		PIC S9(9) COMP.
01  COB-GNAME		PIC S9(4)  COMP VALUE 73.
01  COB-OPEN 		PIC S9(4)  COMP VALUE 192.
01  COB-CLOSE		PIC S9(4)  COMP VALUE 193.
01  COB-GNFN 		PIC S9(4)  COMP VALUE 89.
01  STACKPP	PIC 9(4) COMP.
01  STACKP		PIC 9(4) COMP.
01  STRUCT	OCCURS 10 PIC 9(9) COMP.
01  OPEN-PACK OCCURS 10.
	05  ICH	PIC S9(4) COMP.
	05  ISTI	PIC S9(4) COMP.
	05  ISTO	PIC S9(4) COMP.
	05  IMRS	PIC S9(4) COMP.
	05  IBAD	PIC S9(9) COMP.
	05  IRES	PIC S9(4) COMP.
	05  IRCL	PIC S9(4) COMP.
	05  IRLR	PIC S9(4) COMP.
	05  IRNW	PIC S9(4) COMP.
	05  IRNH	PIC S9(9) COMP.
	05  IFNP	PIC S9(9) COMP.
	05  IDEL	PIC S9(9) COMP.
01  GNFN-PACK OCCURS 10.
	05  NFKY	PIC S9(4) COMP.
	05  NFRS	PIC S9(4) COMP.
	05  NFNM	PIC S9(9) COMP.
	05  NFTP	PIC S9(9) COMP.
01  ADIR	OCCURS 10 PIC X(512).
01  AFILECNT	OCCURS 10 PIC 9(9) COMP.
01  ASUBFILECNT OCCURS 10 PIC 9(9) COMP.
01  GNFN-BUFF		PIC X(32).
01  GNAME-BUFF.
	05  GNAME-BYTE1	PIC X.
	05  GNAME-BYTE2	PIC X.
	05  FILLER	PIC X(510).
01  LINKPLACE		PIC X(512).
01  COB-CDAY		PIC S9(4)  COMP VALUE 218.
01  I 			PIC S9(4) COMP.
01  EOF-SW		PIC X.
	88  EOF		VALUE '1'.
01  TB-ITEM           PIC X(79).
01  SAVE-PATH				PIC X(512).
SCREEN SECTION.
PROCEDURE DIVISION.
0005-CONTROL.
*****
*  NOTHING WORKS IF YOU FORGET THIS!
*****
	IF '<000>' NOT = LOW-VALUES
		DISPLAY 'COMPILE WITH /OCTAL SWITCH PLEASE'
		STOP RUN.
*****
*  SET UP FROM COMMAND LINE, ETC.
*****
	PERFORM 0010-CONTROL-FROM-COMMAND-LINE.

*****
*  SET STARTING DIRECTORY
*****
	IF STARTPATH = SPACE
		NEXT SENTENCE
	ELSE CALL 'GODIR' USING STARTPATH ERRWRD
	     IF ERRWRD NOT = ZERO
		CALL 'IRTN' USING ERRWRD.
*****
*WHO
*****
	CALL "WHO" USING WHO-PID WHO-CON WHO-USER.
*****
* FORM A UNIQUE FILENAME FOR SORT FILE
*****
	MOVE SPACE TO FORSORTFILE OUTSORTFILE PATHWORKFILE.
	MOVE 1 TO SP.
	IF WORKDIR = SPACES
		NEXT SENTENCE
	ELSE
	IF WORKDIR = ':'
		STRING ':' DELIMITED BY SIZE
			INTO FORSORTFILE WITH POINTER SP
	ELSE
	        STRING WORKDIR DELIMITED BY SPACE
		       ':' DELIMITED BY SIZE
 			INTO FORSORTFILE
			WITH POINTER SP.
	STRING
		'?.' DELIMITED BY SIZE
		WHO-USER DELIMITED BY SPACE
		'.' DELIMITED BY SIZE
		WHO-PID DELIMITED BY SIZE
		'.TMP' DELIMITED BY SIZE INTO FORSORTFILE WITH POINTER
		  SP.
	STRING FORSORTFILE DELIMITED BY SPACE
		'.1' DELIMITED BY SIZE
		INTO OUTSORTFILE.
	STRING FORSORTFILE DELIMITED BY SPACE
		'.2' DELIMITED BY SIZE
		INTO PATHWORKFILE.
***
*GET SYSTEM ID
***
	MOVE ZERO TO AC0 AC1.
	MOVE LOW-VALUE TO SYSTEMID.
	CALL '?CBBADDR' USING SYSTEMID AC2.
	CALL '?CBSYS' USING COB-GSID AC0 AC1 AC2 ERR-RET.
	MOVE SPACE TO OUTSYSID.
	STRING SYSTEMID DELIMITED BY LOW-VALUE INTO OUTSYSID.
	ACCEPT WS-DATE FROM DATE.
	MOVE YY TO WYY FYY.
	MOVE MM TO WMM FMM.
	MOVE DD TO WDD FDD.
	CALL 'FDAY' USING FMM FDD FYY TODAYSFDAY.
	MOVE WORKERDATE TO OUTDATE.
	MOVE ZERO TO LINE-CNT PAGE-CNT.
	MOVE ZERO TO JUNKFILETOT.
	MOVE ZERO TO JUNKDELFILETOT JUNKDELFILETOTBYTES.
	MOVE ZERO TO AC1 AC2.
***TURN ON SUPER USER
	MOVE -1 TO AC0.
	CALL '?CBSYS' USING COB-SUPER AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY "I TRIED TO USE SUPERUSER.. IT DIDN'T WORK!".
	EXPUNGE OUTPRINT.
	OPEN OUTPUT OUTPRINT.
	OPEN OUTPUT PATHFILE.
	CLOSE PATHFILE.
	OPEN I-O PATHFILE.
	OPEN OUTPUT FORSORT.
	PERFORM DOHEAD.
	WRITE PLINE FROM
	  '              L E D G E N D                              '.
	WRITE PLINE FROM
	  '                                                         '.
	WRITE PLINE FROM
	  'FILENAME - FROM THE FILE STRUCTURE, ! INDICATE DIRECTORY'.

	WRITE PLINE FROM
	  '           LEVEL, > INDICATES ENTERING A LOWER STRUCTURE.'.
	WRITE PLINE FROM
	  'FNL      - FILE NAME LENGTH, POINTS OUT FILE NAMES > 11 '.
	WRITE PLINE FROM
	  '         - AND > 27.       '.
	WRITE PLINE FROM
	  'TYPE     - AOS/VS FILE TYPE                              '.
	WRITE PLINE FROM
	  'CREATED  - CREATION DATE,  LOOK FOR OLD FILES!          '.
	WRITE PLINE FROM
	  'MODIFIED - LAST ACCESSED,  HOW LONG HAS IT BEEN?        '.
	WRITE PLINE FROM
	  'ACCESSED - DATE LAST ACCESSED, IS IT BEING USED OR JUST '.
	WRITE PLINE FROM
	  '           BACKED UP.                                 '.
	WRITE PLINE FROM
	  '****USE SWITCH /S TO GET DATES AS DAYS SINCE          '.
	WRITE PLINE FROM
	  'SIZE     - FILE SIZE IN BYTES, HOW BIG IS IT?            '.
	WRITE PLINE FROM
	  'INDEX    - FILESYSTEM INDEX, CURRENT OUT OF POSSIBLE,  '.
	WRITE PLINE FROM
	  '           LOOK FOR 2/3 OR 3/3.  THIS COULD INDICATE A   '.
	WRITE PLINE FROM
	  '           PROBLEM.  CONSIDER A LARGER ELEMENT SIZE FOR '.
	WRITE PLINE FROM
	  '           FILES.  FOR A DIRECTORY CONSIDER BREAKING IT '.
	WRITE PLINE FROM
	  '           UP INTO SMALLER DIRECTORIES.'.
	WRITE PLINE FROM
	  'ELEMENT  - THE BUILDING BLOCKS OF FILES.  HOW BIG SHOULD'.
	WRITE PLINE FROM
	  '           THE BLOCK BE?'.
	WRITE PLINE FROM
	  'OPEN     - OPEN COUNT, DO YOU KNOW WHAT FILES ARE IN USE?'.
	WRITE PLINE FROM
	  'FLAGS***************************************************'.
	WRITE PLINE FROM
	  'SHR      - SHARED FILE '.
	WRITE PLINE FROM
	  'MOD      - MODIFIED'.
	WRITE PLINE FROM
	  'PRM      - FILE HAS THE PERMANENT ATTRIBUTE SET'.
	WRITE PLINE FROM
	  'DOC      - DELETE ON CLOSE'.
	WRITE PLINE FROM
	  'UDA      - FILE HAS A USER DATA AREA'.
	WRITE PLINE FROM
	  'URA      - ALL USERS HAVE READ ACCESS'.
	WRITE PLINE FROM
	  'UEA      - ALL USERS HAVE EXECUTE ACCESS'.
	PERFORM DOHEAD.
	MOVE ZERO TO STACKP.
	MOVE LOW-VALUE TO GNAME-BUFF.
	STRING '=' DELIMITED BY SIZE INTO GNAME-BUFF.
	CALL '?CBBADDR' USING GNAME-BUFF AC0.
	CALL '?CBBADDR' USING GNAME-BUFF AC1.
	MOVE 512 TO AC2.
	CALL '?CBSYS' USING COB-GNAME AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO THEN
		DISPLAY 'GNAME CALL FOR INITIAL DIRECTORY'
		CALL 'IRTN' USING ERR-RET.
***GNAME-BUFF HAS THE COMPLETE PATH NAME OF THE STARTING POINT.
*THE ROOT IS A SPECIAL CASE.
	IF GNAME-BYTE1 = ':' AND
		GNAME-BYTE2 = LOW-VALUE
		MOVE '1' TO ONEISROOT-SW
	ELSE MOVE ' ' TO ONEISROOT-SW.

	MOVE ZERO TO DIRECTORYCOUNTER.
	PERFORM PUSHIT.
	PERFORM 0035-READ UNTIL STACKP = 0.
	MOVE JUNKFILETOT TO OUTJUNKFILETOT.
	MOVE JUNKDELFILETOT TO OUTJUNKDELFILETOT.
	MOVE JUNKDELFILETOTBYTES TO OUTJUNKDELFILETOTBYTES.
	MOVE DIRECTORYCOUNTER TO OUTDIRECTORYCOUNTER.
	MOVE ' '      TO PLINE.
	PERFORM LINEOUT.
	MOVE STATHEAD TO PLINE.
	PERFORM LINEOUT.
	MOVE STATLINE TO PLINE.
	PERFORM LINEOUT.
	CLOSE FORSORT.
	SORT OUTSORT ON ASCENDING KEY
		  USING FORSORT
		  GIVING FORSORT.
	PERFORM DUPLICATE-REPORT.
	CLOSE PATHFILE.
	EXPUNGE PATHFILE.
	CLOSE OUTPRINT.
	STOP RUN.

0035-READ.
***               GNFN
	CALL '?CBBADDR' USING GNFN-BUFF NFNM (STACKP).
	MOVE ICH (STACKP) TO AC1.
***		AC2 GETS ADDRESS OF GNFN PACKET
	CALL '?CBADDR' USING GNFN-PACK (STACKP) AC2.
	CALL '?CBSYS' USING COB-GNFN AC0 AC1 AC2 ERR-RET.
	IF ERR-RET = 24
		THEN PERFORM POPIT
	ELSE
	IF ERR-RET NOT = ZERO
		DISPLAY 'GNFN CALL FOR FILENAMES'
		CALL 'ERMSG' USING ERR-RET ER-MES ERR-RET
		DISPLAY ER-MES
		PERFORM POPIT
	ELSE
		PERFORM 0045-GET-CREATION.
0035-EXIT. EXIT.

0045-GET-CREATION.
* GET PATHNAME OF FILE FOR INPUT TO FILESTATUS.
	MOVE LOW-VALUES TO GNAME-BUFF.
	IF STACKP = 1 AND ONEISROOT THEN
		STRING ':' DELIMITED BY SIZE
		GNFN-BUFF DELIMITED BY LOW-VALUE INTO GNAME-BUFF
	ELSE
	       STRING ADIR (STACKP) DELIMITED BY LOW-VALUE
		':' DELIMITED BY SIZE
		GNFN-BUFF DELIMITED BY LOW-VALUE INTO GNAME-BUFF.
********
*THIS RESOLVES LINKS ... I DON'T WANT TO DO THAT
*CALL '?CBBADDR' USING GNAME-BUFF AC0.
*CALL '?CBBADDR' USING GNAME-BUFF AC1.
*MOVE 512 TO AC2.
*CALL '?CBSYS' USING COB-GNAME AC0 AC1 AC2 ERR-RET.
*IF ERR-RET NOT = ZERO
*	THEN DISPLAY 'GNAME FOR VALIDATION OF FILENAME'
*	     DISPLAY GNAME-BUFF
*	     CALL 'IRTN' USING ERR-RET.
	MOVE ZERO TO STYP SCPS SLAU SDEH  SMIL STCH STCL STAH
	 STAL STMH STML SSTS SEFW  SEFM  SFAH SFAL SIDX SOPN.
	MOVE -1 TO STIM SACP.
	CALL '?CBBADDR' USING GNAME-BUFF AC0.
****SET TO IGNORE LINKS
**** BIT 1 = 1 TO IGNORE LINKS
	MOVE ZERO TO A1BIT.
	MOVE 64 TO DIDB1.
	MOVE A1BIT TO AC1.
	CALL '?CBADDR' USING FSTAT-PACK AC2.
	CALL '?CBSYS' USING COB-GSTAT AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY 'FILESTATUS CALL'
		DISPLAY GNAME-BUFF
		CALL 'IRTN' USING ERR-RET.
	ADD 1 TO AFILECNT (STACKP) ASUBFILECNT (STACKP).
	MOVE SPACE TO OUTFILEN.
	MOVE 1 TO PP.
	COMPUTE STACKPP = STACKP - 1.
	PERFORM A-BANG  STACKPP    TIMES.
	IF STYP= A-DISK OR A-CPD OR A-DIR THEN
		MOVE ALL '-' TO OUTELEX
		STRING '>' DELIMITED BY SIZE
		  INTO OUTFILEN WITH POINTER PP
	ELSE
		MOVE ALL '-' TO OUTELEX
		STRING '!' DELIMITED BY SIZE
		  INTO OUTFILEN WITH POINTER PP
		MOVE SDEH TO OUTELEMENT.
	STRING
		GNFN-BUFF DELIMITED BY LOW-VALUE
		INTO OUTFILEN WITH POINTER PP.
******
*HOW LONG IS THE FILENAME
******
	MOVE 0 TO PP.
	INSPECT GNFN-BUFF TALLYING PP FOR CHARACTERS BEFORE INITIAL 
		LOW-VALUE.
	IF PP > 27 THEN
		MOVE '>27' TO FNL
	ELSE
	IF PP > 11 THEN
		MOVE '>11' TO FNL
	ELSE MOVE SPACE TO FNL.
	SET TNDX TO 1.
	SEARCH FILETYPE VARYING TNDX
		AT END MOVE LOW-VALUE TO TMPPLACE
			MOVE STYPX TO TEMP1X
			MOVE TEMP1 TO OUTTYPE
		WHEN FT  (TNDX) = STYPX
		   MOVE FTNAME (TNDX) TO OUTTYPEX.
	IF SINCE
	   	COMPUTE TEMP1 = TODAYSFDAY - STCH
	   	MOVE TEMP1 TO SINCECREATED
	   	COMPUTE TEMP1 = TODAYSFDAY - STAH
	   	MOVE TEMP1 TO SINCEACCESSED
	   	COMPUTE TEMP1 = TODAYSFDAY - STMH
	   	MOVE TEMP1 TO SINCEMODIFIED
	ELSE
		MOVE STCH TO FRETURN
		PERFORM RECLAIMDATE
		MOVE WORKERDATE TO OUTCREATED
		MOVE STAH TO FRETURN
		PERFORM RECLAIMDATE
		MOVE WORKERDATE TO OUTACCESSED
		MOVE STMH TO FRETURN
		PERFORM RECLAIMDATE
		MOVE WORKERDATE TO OUTMODIFIED.
	MOVE SEFM TO OUTSIZE.
	MOVE SMIL TO OMIL.
	MOVE '/' TO OSLSH.
	MOVE SIDX TO OSIDX.
	MOVE SOPN TO OUTOPEN.
	PERFORM FLAGLOGIC.
	IF OUTTYPEX = 'IPC'
		MOVE ALL '-' TO OVERLAYREC
	ELSE
	IF OUTTYPEX = 'LNK'
		MOVE SPACE TO OVERLAYREC
		PERFORM LNKWHERE.
	MOVE OUTDETAIL TO PLINE.
	PERFORM LINEOUT.
	MOVE STRUCT (STACKP)  TO OS-STRUCT.
	MOVE SPACE TO OS-FILE.
	STRING GNFN-BUFF DELIMITED BY LOW-VALUE INTO
		 OS-FILE.
	MOVE OUTTYPEX TO OS-TYPE.
	WRITE FORSORTREC.
	MOVE SPACE TO OUTDETAIL.
	IF STYP = A-DIR OR A-CPD OR A-DISK
		PERFORM PUSHIT.
0045-EXIT. EXIT.

LNKWHERE.
	CALL '?CBBADDR' USING GNAME-BUFF AC0.
	CALL '?CBBADDR' USING LINKPLACE AC1.
	MOVE ZERO TO AC2.
	CALL '?CBSYS' USING COB-GLINK AC0 AC1 AC2 ERR-RET.
	IF ERR-RET  = ZERO THEN
		STRING LINKPLACE DELIMITED BY LOW-VALUE INTO
			OVERLAYREC.
	
RECLAIMDATE.
	CALL 'CDAY' USING FMM FDD FYY FRETURN.
	MOVE FMM TO WMM.
	MOVE FDD TO WDD.
	MOVE FYY TO WYY.

A-BANG.
	STRING '!' DELIMITED BY SIZE INTO
		OUTFILEN WITH POINTER PP.
FLAGLOGIC.
****
*****
	MOVE SPACE TO OSHARE OMOD OPERM ODOC OUDA OURA OUEA.
	MOVE LOW-VALUES TO SSTSWORKX.
	MOVE SSTSX TO SSTSWORKLOWX.
	IF SSTSWORK NOT < 32768
* BIT FOR FILE IS SHARED
		MOVE 'X' TO OSHARE
		SUBTRACT 32768 FROM SSTSWORK.
	IF SSTSWORK NOT < 16384
* BIT FOR FILE HAS BEEN MODIFIED
		MOVE 'X' TO OMOD
		SUBTRACT 16384 FROM SSTSWORK.
	IF SSTSWORK NOT < 1024
* BIT FOR FILE IS PERM
		MOVE 'X' TO OPERM
		SUBTRACT 1024 FROM SSTSWORK.
	IF SSTSWORK NOT < 512
* BIT FOR DELETE FILE ON CLOSE
		MOVE 'X' TO ODOC
		SUBTRACT 512 FROM SSTSWORK.
	IF SSTSWORK NOT < 256
* BIT FOR FILE HAS A UDA
		MOVE 'X' TO OUDA
		SUBTRACT 256 FROM SSTSWORK.
	IF SSTSWORK NOT < 2
* BIT FOR FILE USER HAS READ
		MOVE 'X' TO OURA
		SUBTRACT 2   FROM SSTSWORK.
	IF SSTSWORK NOT < 1
* BIT FOR FILE USER HAS EXE
		MOVE 'X' TO OUEA
		SUBTRACT 1   FROM SSTSWORK.

PUSHIT.
	ADD  1 TO STACKP.
	ADD 1 TO DIRECTORYCOUNTER.
	MOVE DIRECTORYCOUNTER TO XREL1.
	MOVE DIRECTORYCOUNTER TO STRUCT (STACKP).
	MOVE SPACE TO TB-ITEM.
	MOVE ZERO TO AFILECNT (STACKP) ASUBFILECNT (STACKP).
	MOVE GNAME-BUFF TO ADIR (STACKP) SAVE-PATH.
	WRITE PATHBUFFER FROM SAVE-PATH.
***		OPEN THIS DIRECTORY
	MOVE ZERO TO AC0 AC1.
	MOVE 17 TO ISTI (STACKP).
	MOVE ZERO TO ICH (STACKP).
	MOVE ZERO TO ISTI (STACKP).
	MOVE ZERO TO ISTO (STACKP).
	MOVE -1 TO  IMRS (STACKP).
	MOVE -1 TO IBAD	 (STACKP).
	MOVE ZERO TO IRES (STACKP).
	MOVE -1 TO IRCL	(STACKP).
	MOVE ZERO TO IRLR (STACKP).
	MOVE ZERO TO  IRNW (STACKP).
	MOVE ZERO TO IRNH (STACKP).
	MOVE ZERO TO IFNP (STACKP).
	MOVE -1 TO IDEL (STACKP).
	CALL '?CBBADDR' USING SAVE-PATH IFNP (STACKP).
*?OFIN + ?RTDY GO TO ISTI.
	CALL '?CBADDR' USING OPEN-PACK (STACKP)  AC2.
	CALL '?CBSYS' USING COB-OPEN AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY 'OPEN CALL FOR DIRECTORY'
		DISPLAY SAVE-PATH
		CALL 'IRTN' USING ERR-RET.
	MOVE ZERO TO NFKY (STACKP).
	MOVE LOW-VALUES TO TEMPLATE.
	STRING '+' DELIMITED BY SIZE
		        INTO TEMPLATE.
	CALL '?CBBADDR' USING TEMPLATE NFTP (STACKP).
ENDPUSHIT. EXIT.

POPIT.
*CLOSE DIRECTORY.
	IF STACKP = 1
		NEXT SENTENCE
	ELSE ADD ASUBFILECNT (STACKP) TO ASUBFILECNT (STACKP - 1).
	CALL '?CBADDR' USING OPEN-PACK (STACKP) AC2.
	CALL '?CBSYS' USING COB-CLOSE AC0 AC1 AC2 ERR-RET.
 	PERFORM RESCANFORDELETES.
	SUBTRACT 1 FROM STACKP.

0010-CONTROL-FROM-COMMAND-LINE.
	MOVE 0 TO G-ARGUMENT-NO.
	MOVE 'S' TO G-SWITCH.
	CALL 'GET' USING GTSW G-AC0 G-AC1 G-ARGUMENT-NO
		G-SWITCH
		G-BUFFER G-ERRWRD.
	IF G-ERRWRD = ZERO THEN
		IF G-AC0 = 0 THEN
			MOVE '1' TO SINCE-SW
		ELSE
		        MOVE ' ' TO SINCE-SW
	ELSE
		DISPLAY 'GET SWITCH'
		CALL 'IRTN' USING G-ERRWRD.
	MOVE 'L' TO G-SWITCH.
	CALL 'GET' USING GTSW G-AC0 G-AC1 G-ARGUMENT-NO G-SWITCH
			   G-BUFFER G-ERRWRD.
	IF G-ERRWRD  = NO-ARG
		THEN
		     PERFORM SHOW-HIM-HOW
		     STOP RUN
	ELSE
	IF G-ERRWRD NOT = ZERO
		     DISPLAY 'GET '
		     CALL 'IRTN' USING G-ERRWRD.
	IF G-AC0 NOT > 0 THEN
		MOVE '@OUTPUT' TO OUTPRINTFILE
	ELSE 	MOVE SPACES TO OUTPRINTFILE
		STRING G-BUFFER DELIMITED BY LOW-VALUE
 		INTO OUTPRINTFILE.
	MOVE 'W' TO G-SWITCH.
	CALL 'GET' USING GTSW G-AC0 G-AC1 G-ARGUMENT-NO G-SWITCH
			   G-BUFFER G-ERRWRD.
	IF G-ERRWRD  = NO-ARG
		THEN
		     PERFORM SHOW-HIM-HOW
		     STOP RUN
	ELSE
	IF G-ERRWRD NOT = ZERO
		     DISPLAY 'GET '
		     CALL 'IRTN' USING G-ERRWRD.
	IF G-AC0 NOT > 0 THEN
		MOVE SPACES   TO WORKDIR
	ELSE 	MOVE SPACES TO WORKDIR
		STRING G-BUFFER DELIMITED BY LOW-VALUE
 		INTO WORKDIR.
	MOVE 'P' TO G-SWITCH.
	CALL 'GET' USING GTSW G-AC0 G-AC1 G-ARGUMENT-NO G-SWITCH
			   G-BUFFER G-ERRWRD.
	IF G-ERRWRD  = NO-ARG
		THEN
		     PERFORM SHOW-HIM-HOW
		     STOP RUN
	ELSE
	IF G-ERRWRD NOT = ZERO
		     DISPLAY 'GET '
		     CALL 'IRTN' USING G-ERRWRD.
	IF G-AC0 NOT > 0 THEN
		MOVE SPACES   TO STARTPATH
	ELSE 	MOVE SPACES TO STARTPATH
		STRING G-BUFFER DELIMITED BY LOW-VALUE
 		INTO STARTPATH.
	MOVE 0 TO G-ARGUMENT-NO.
	MOVE LOW-VALUES TO TEMPLATETABLE.
	MOVE ZERO TO G-ERRWRD HIGHEST-TEMP.
	PERFORM GET-ARG-FROM-LINE UNTIL G-ERRWRD = NO-ARG.
	IF HIGHEST-TEMP = ZERO THEN
		PERFORM SHOW-HIM-HOW
		STOP RUN.

SHOW-HIM-HOW.
	DISPLAY '        XF .... e-X-tended -F-ilestatus'.
	DISPLAY ' '.
	DISPLAY 'USAGE: X XF[/W=/L=/P=/S] TEMPLATE[/D]'
	  ' [...TEMPLATE[/D]]'.
	DISPLAY ' '.
	DISPLAY 'Where W= Working Directory...use this directory'
	        ' for temporary files.'
	DISPLAY '      L= Listing File ... send the report to this'
	        ' destination.'.
	DISPLAY '      P= Starting Path ...start in this directory'
	        ' when you create my report.'.
	DISPLAY '      /S ...Use days since (scaler) on the report'
                ' instead of date format.'.
	DISPLAY '      TEMPLATE/D..upto 32 templates..find these'
	        ' and optionally delete them.'.
	DISPLAY ' '.
	DISPLAY 'EXAMPLE: X XF/S/W=:UDD:WORK/P=:/L=:UDD:WORK:?OUT +.ED'.
HEAD2.
	IF OKTODEL (I) THEN
		MOVE  DELETED-MESS TO PLINE
	ELSE MOVE WOULDHAVE-MESS TO PLINE.
	PERFORM LINEOUT.
	MOVE DET-HEAD TO PLINE.
	PERFORM LINEOUT.
	MOVE ' '      TO PLINE.
	PERFORM LINEOUT.

GET-ARG-FROM-LINE.
	ADD 1 TO G-ARGUMENT-NO.
	CALL 'GET' USING GARG G-AC0 G-AC1 G-ARGUMENT-NO G-SWITCH
			   G-BUFFER G-ERRWRD.
	IF G-ERRWRD = ZERO THEN
	   IF G-ARGUMENT-NO > 32 THEN
		DISPLAY '32 TEMPLATES MAXIMUM'
		STOP RUN
	   ELSE
		ADD 1 TO HIGHEST-TEM

		MOVE   G-BUFFER TO ATEMPLATE (G-ARGUMENT-NO)
******
*CHECK FOR A D SWITCH ... IT GIVES THE GO AHEAD.
******
		MOVE 'D' TO G-SWITCH
		CALL 'GET' USING GTSW G-AC0 G-AC1 G-ARGUMENT-NO
			G-SWITCH
			G-BUFFER G-ERRWRD
		IF G-ERRWRD = ZERO THEN
			IF G-AC0 = 0 THEN
			  MOVE '1' TO OKTODEL-SW (G-ARGUMENT-NO)
			ELSE
			  MOVE ' ' TO OKTODEL-SW (G-ARGUMENT-NO)
		ELSE
		     DISPLAY 'GET SWITCH'
		     CALL 'IRTN' USING G-ERRWRD
	ELSE
	IF G-ERRWRD  = NO-ARG
		NEXT SENTENCE
	ELSE
	IF G-ERRWRD NOT = ZERO
		DISPLAY 'GET '
		     CALL 'IRTN' USING G-ERRWRD.
**********************************
RESCANFORDELETES.
	MOVE ' '      TO PLINE.
	PERFORM LINEOUT.
	STRING 'SUMMARY--> ' DELIMITED BY SIZE
		ADIR (STACKP) DELIMITED BY LOW-VALUE
		INTO PLINE.
	PERFORM LINEOUT.
	PERFORM TELLWHATTYPE.
	IF STYP = A-CPD OR A-DISK THEN
	   IF SMSH = -1
		MOVE ZERO TO OMAX OPER OREM
		MOVE SCHS TO OCUR
	   ELSE
		MOVE SMSH TO OMAX
		MOVE SCHS TO OCUR
		COMPUTE BIGTEMP = SMSH - SCHS
		MOVE BIGTEMP TO OREM
		IF SCHS = ZERO
			MOVE ZERO TO OPER
		ELSE
			COMPUTE BIGTEMP ROUNDED = SCHS / SMSH * 10
			MOVE BIGTEMP TO OPER
	ELSE MOVE ZERO TO OMAX OCUR OPER OREM.
	MOVE STRUCT (STACKP)  TO ODIRCNT.
	MOVE AFILECNT (STACKP) TO OFCNT.
	MOVE ASUBFILECNT (STACKP) TO OSCNT.
	MOVE DIRLINE1 TO PLINE.
	PERFORM LINEOUT.
	MOVE DIRLINE2 TO PLINE.
	PERFORM LINEOUT.
	MOVE ' '      TO PLINE.
	PERFORM LINEOUT.
	PERFORM 1MOREOPEN.
	PERFORM RESCANIT VARYING I FROM 1 BY 1 UNTIL I > HIGHEST-TEMP.
	PERFORM CLOSEIT.

RESCANIT.
	PERFORM STARTTHISTEMPLATE.
	MOVE ' ' TO FINISHED-SW.
	PERFORM RESCANTHISDIR UNTIL FINISHED.

1MOREOPEN.
*********
* A WORD OF EXPLANATION.... AOS/VS DOES NOT ALLOW SUB DIRECTORIES
* BEYOND 8 LEVELS BELOW THE ROOT, OPEN PACKET 10 SHOULD NOT BE
* USED WHEN TRAVERSING THE DIRECTORY STRUCTURE SO I'M USING IT NOW.
**********
***		OPEN THIS DIRECTORY
	MOVE ZERO TO AC0 AC1.
	MOVE 17 TO ISTI (10).
	MOVE ZERO TO ICH (10).
	MOVE ZERO TO ISTI (10).
	MOVE ZERO TO ISTO (10).
	MOVE -1 TO  IMRS (10).
	MOVE -1 TO IBAD	 (10).
	MOVE ZERO TO IRES (10).
	MOVE -1 TO IRCL (10).
	MOVE ZERO TO IRLR (10).
	MOVE ZERO TO  IRNW (10).
	MOVE ZERO TO IRNH (10).
	MOVE ZERO TO IFNP (10).
	MOVE -1 TO IDEL (10).
	CALL '?CBBADDR' USING ADIR (STACKP) IFNP (10).
*?OFIN + ?RTDY GO TO ISTI.
	CALL '?CBADDR' USING OPEN-PACK (10) AC2.
	CALL '?CBSYS' USING COB-OPEN AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY 'OPEN CALL FOR DIRECTORY'
		DISPLAY ADIR (STACKP)
		CALL 'IRTN' USING ERR-RET.

STARTTHISTEMPLATE.
	MOVE '1' TO FIRSTPRINT-SW.
	MOVE SPACE TO OUTTEMPLATE1.
	STRING ATEMPLATE (I) DELIMITED BY LOW-VALUE INTO
		OUTTEMPLATE1.
	MOVE OUTTEMPLATE1 TO OUTTEMPLATE2.
	MOVE ZERO TO NFKY (10).
	MOVE LOW-VALUES TO DELTEMPLATE.
	STRING ATEMPLATE (I) DELIMITED BY SIZE
		        INTO DELTEMPLATE.
	CALL '?CBBADDR' USING DELTEMPLATE NFTP (10).

RESCANTHISDIR.
***               GNFN
	CALL '?CBBADDR' USING RBUF  NFNM (10).
	MOVE ICH (10)  TO AC1.
***		AC2 GETS ADDRESS OF GNFN PACKET
	CALL '?CBADDR' USING GNFN-PACK (10)  AC2.
	CALL '?CBSYS' USING COB-GNFN AC0 AC1 AC2 ERR-RET.
	IF ERR-RET = 24
		THEN MOVE '1' TO FINISHED-SW
	ELSE
	IF ERR-RET NOT = ZERO
		DISPLAY 'GNFN CALL FOR FILENAMES'
		CALL 'ERMSG' USING ERR-RET ER-MES ERR-RET
		DISPLAY ER-MES
		MOVE '1' TO FINISHED-SW
	ELSE
		PERFORM DEALWITHIT.

DEALWITHIT.
	ADD 1 TO JUNKFILETOT.
	MOVE '*NONE*' TO DET-ACTION.
	MOVE SPACES TO DET-FILE.
	STRING RBUF  DELIMITED BY LOW-VALUE INTO DET-FILE.
	IF OKTODEL (I) THEN
		MOVE LOW-VALUE TO KILLBUFFER
		IF STACKP = 1 AND ONEISROOT THEN
		   STRING ':' DELIMITED BY SIZE
		   RBUF DELIMITED BY LOW-VALUE INTO KILLBUFFER
		ELSE
		   STRING ADIR (STACKP) DELIMITED BY LOW-VALUE
			':' DELIMITED BY SIZE
			RBUF DELIMITED BY LOW-VALUE INTO KILLBUFFER
		END-IF
		CALL '?CBBADDR' USING KILLBUFFER  AC0
		PERFORM AFILESTAT
		MOVE ZERO TO AC1 AC2
		CALL '?CBBADDR' USING KILLBUFFER AC0
		CALL '?CBSYS' USING COB-DELETE AC0 AC1 AC2 ERR-RET
		MOVE ZERO TO ERR-RET
		IF ERR-RET  = ZERO THEN
			MOVE 'DELETED' TO DET-ACTION
			ADD 1 TO JUNKDELFILETOT
			ADD SEFM TO JUNKDELFILETOTBYTES
		ELSE
		IF ERR-RET  = PERMON THEN
			MOVE 'PERM' TO DEL-ACTION
		ELSE DISPLAY 'DELETE ' KILLBUFFER
		     CALL 'IRTN' USING ERR-RET.
	IF FIRSTPRINT THEN
		PERFORM HEAD2
		MOVE ' ' TO FIRSTPRINT-SW.
	MOVE DET-LINE TO PLINE.
	PERFORM LINEOUT.


CLOSEIT.
	CALL '?CBADDR' USING OPEN-PACK (10) AC2.
	CALL '?CBSYS' USING COB-CLOSE AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY 'CLOSE'
		CALL 'IRTN' USING ERR-RET.

TELLWHATTYPE.
* GET PATHNAME-INPUT TO FILESTATUS.
	CALL '?CBBADDR' USING ADIR (STACKP) AC0.
	PERFORM AFILESTAT.
	IF STYP = A-DISK THEN
		MOVE SPACE TO CALLSTR
		MOVE ' '      TO PLINE
		PERFORM LINEOUT
		STRING ADIR (STACKP) DELIMITED BY LOW-VALUE INTO
			CALLSTR
		CALL 'BIGCH' USING BIGPARM
		PERFORM WRITEITBIG VARYING BIGI FROM 1 BY 1 UNTIL
			BIGI > 7
		MOVE ' '      TO PLINE
		PERFORM LINEOUT
	ELSE
	IF STYP = A-CPD OR A-DIR
		MOVE SCPS TO OHASH.
WRITEITBIG.
	MOVE ' '  TO PLINE.
	STRING BIGLINE (BIGI) DELIMITED BY NEWLINER
		INTO PLINE.
	PERFORM LINEOUT.

DUPLICATE-REPORT.
	OPEN INPUT FORSORT.
	PERFORM DOHEAD2.
	MOVE ' ' TO EOF-SW.
	MOVE SPACE TO LOOKAHEADBUF.
	PERFORM GET-A-REC.
	PERFORM DO-REPORT UNTIL EOF.
	CLOSE FORSORT.
	EXPUNGE FORSORT.

DO-REPORT.
***
*START OF OS-FILE
***
	MOVE OS-FILE TO HOLDOS-FILE OUTOS-FILE.
	PERFORM LOOK-AHEAD.
	IF PRINT-ON
		MOVE OS-FILE TO PLINE
		PERFORM LINEOUT2.
	PERFORM DO-OS-FILE UNTIL EOF OR HOLDOS-FILE NOT = OS-FILE.
	IF PRINT-ON
		MOVE ' ' TO PLINE
		PERFORM LINEOUT2.
***
*END OF OS-FILE
***

DO-OS-FILE.
	IF PRINT-ON
		MOVE OS-STRUCT TO XREL1
		READ PATHFILE
		UNSTRING PATHBUFFER DELIMITED BY LOW-VALUE
			INTO STR1 STR2 STR3
		PERFORM OUTLONG.
	PERFORM GET-A-REC.

OUTLONG.
	IF STR1 NOT = SPACE
		STRING '  ' DELIMITED BY SIZE
		OS-TYPE DELIMITED BY SIZE
		' ' DELIMITED BY SIZE
		STR1  DELIMITED BY SIZE
		   INTO PLINE
		PERFORM LINEOUT2.
	IF STR2 NOT = SPACE
		STRING '    ' DELIMITED BY SIZE
		STR2  DELIMITED BY SIZE
		   INTO PLINE
		PERFORM LINEOUT2.
	IF STR3 NOT = SPACE
		STRING '      ' DELIMITED BY SIZE
		STR3  DELIMITED BY SIZE
		   INTO PLINE
		PERFORM LINEOUT2.

LOOK-AHEAD.
	MOVE FORSORTREC TO HOLDFORSORTREC.
	PERFORM GET-A-REC.
	IF EOF
		MOVE SPACE TO LOOKAHEADBUF
	ELSE MOVE FORSORTREC TO LOOKAHEADBUF.
	MOVE HOLDFORSORTREC TO FORSORTREC.
	IF LA-FILE = OS-FILE
**DUPLICATE FOUND
		MOVE '1' TO PRINT-ON-SW
	ELSE    MOVE ' ' TO PRINT-ON-SW.

GET-A-REC.
	IF LA-FILE = SPACE
		READ FORSORT AT END MOVE '1' TO EOF-SW
	ELSE MOVE LOOKAHEADBUF TO FORSORTREC
	     MOVE SPACE TO LOOKAHEADBUF.

LINEOUT2.
	IF LINE-CNT > LINE-LIMIT
		PERFORM DOHEAD2.
	ADD 1 TO LINE-CNT.
	WRITE PLINE.
DOHEAD2.
	MOVE PLINE TO SAVEPLINE.
	MOVE 3  TO LINE-CNT.
	ADD 1 TO PAGE-CNT.
	WRITE PLINE FROM HEADTIP AFTER PAGE.
	WRITE PLINE FROM HEADDUP.
	WRITE PLINE FROM ' '.
	WRITE PLINE FROM HEADERDUP.
	WRITE PLINE FROM ' '.
	MOVE SAVEPLINE TO PLINE.

LINEOUT.
	IF LINE-CNT > LINE-LIMIT
		PERFORM DOHEAD.
	ADD 1 TO LINE-CNT.
	WRITE PLINE.
DOHEAD.
	MOVE PLINE TO SAVEPLINE.
	MOVE 7  TO LINE-CNT.
	ADD 1 TO PAGE-CNT.
	WRITE PLINE FROM HEADTIP AFTER PAGE.
	WRITE PLINE FROM ' '.
	MOVE FLAGD1 TO PFLAGS.
	WRITE PLINE FROM HEADX1.
	MOVE FLAGD2 TO PFLAGS.
	WRITE PLINE FROM HEADX1.
	MOVE FLAGD3 TO PFLAGS.
	WRITE PLINE FROM HEADX1.
	WRITE PLINE FROM HEADTOP.
	WRITE PLINE FROM ' '.
	MOVE SAVEPLINE TO PLINE.

AFILESTAT.
	MOVE ZERO TO STYP SCPS SLAU SDEH  SMIL STCH STCL STAH
	 STAL STMH STML SSTS SEFW  SEFM  SFAH SFAL SIDX SOPN.
	MOVE -1 TO STIM SACP.
****SET TO IGNORE LINKS
**** BIT 1 = 1 TO IGNORE LINKS
	MOVE ZERO TO A1BIT.
	MOVE 64 TO DIDB1.
	MOVE A1BIT TO AC1.
	CALL '?CBADDR' USING FSTAT-PACK AC2.
	CALL '?CBSYS' USING COB-GSTAT AC0 AC1 AC2 ERR-RET.
	IF ERR-RET NOT = ZERO
		DISPLAY 'FILESTATUS CALL'
		DISPLAY ADIR (STACKP)
		CALL 'IRTN' USING ERR-RET.
********
*LAST LINE OF PROGRAM
