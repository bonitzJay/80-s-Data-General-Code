.TITLE W?O
        .ENT W?O

; THIS IS AN ASSEMBLY LANGUAGE SUBPROGRAM CALLABLE FROM
; A COBOL PROGRAM. IT RETURNS PROCESS ID AND CONSOLE OR
; STREAM NAME AND USERNAME.
; CALLING SEQUENCE:
;  CALL "W?O" USING <PID-VARIABLE> <CONSOLE-VARIABLE> <USERNAME-VARIABLE>

        .NREL   0                       ; UNSHARED CODE


; PARAMETER TABLE

.PID =  -12.                    ; BECOMES BYTE ADDRESS OF PID VARIABLE
.PIDA = 202                     ; CIS ATTR(TYPE=UNSIGNED,LEN=3)

.CON =  -14.                    ; BECOMES BYTE ADDRESS OF CONSOLE VARIABLE
.CONL = 32.                     ; BYTE LENGTH OF CONSOLE VARIABLE

.USER = -16.                    ;BECOMES BYTE ADDRESS OF USERNAME VARIABLE
.USERL = 32.                    ;BYTE LENGTH OF USERNAME VARIABLE

; AOS/vs PACKET FOR ?EXEC CALL
APACK:
**      .DO ?XLTH
        .SWORD 0
**      .ENDC

; TEMPORARY BUFFER FOR CONSOLE NAME
.BUF:   BUF*2
BUF:    .BLK    16.
BUF2:   .BLK    ?MXUN

; 16 WORD TRANSLATION TABLE FOR CMT INSTRUCTION
; BIT FOR NULL IS ON, ALL OTHERS ARE OFF
TRAN:   .WORD 100000
**      .DO 15.
        .SWORD 0
**      .ENDC
        .NREL   1
 ; SUBPROGRAM STARTS HERE
W?O:    WSAVS   0

; MAKE AOS/vs CALL TO GET PROCESS ID
        WSUB    0,0             ; AC0 GET 0
        WADC    1,1             ; AC1 GETS -1
        ?PNAME
        WBR     .+1             ; ERROR NOT POSSIBLE

; PID IS IN AC1, FLOAT TO FPAC0
        WFLAD   1,0

; STORE THE PID IN THE PID VARIABLE
        NLDAI   .PIDA,1         ; CIS ATTRIBUTE OF PID
        XWLDA   3,.PID,3        ; BYTE ADDRESS OF PID VARIABLE
        WSTI    0
        LDAFP   3


; MAKE AOS/vs CALL TO GET CONSOLE NAME TO BUF
        LLEF    2,APACK         ; AOS/vs PACKET ADDRESS TO AC2
        NLDAI   ?XFSTS,0        ; EXEC FUNCTION IS STATUS
        XNSTA   0,?XRFNC,2
        LWLDA   0,.BUF          ; TEMP BUFFER BYTE ADDR TO PACKET
        XWSTA   0,?XFP2,2
        ?EXEC
        WBR     DONE            ; QUIT IF ERROR

; MOVE CONSOLE NAME FROM BUF TO <CONSOLE> VARIABLE
; MOVE CHARACTERS UNTIL NULL ENCOUNTE

        LLEF    0,TRAN          ; TRANSLATION TABLE WORD ADDR
        NLDAI   .CONL,1         ; BYTE LENGTH OF CONSOLE VARIABLE
        XWLDA   2,.CON,3        ; DESTINATION BYTE ADDR
        LWLDA   3,.BUF          ; SOURCE BYTE ADDRESS
        WCMT

;MAKE ?GUNM CALL TO GET USERNAME TO BUF
        WADC    0,0             ;AC0 GETS -1
        WSUB    1,1             ;AC1 GETS ZERO
        LLEFB   2,BUF2*2        ;BP TO USERNAME BUFF
        ?GUNM
        WBR     DONE            ;QUIT ON ERROR
        LDAFP   3
        WLDAI   ?MXUN*2,1       ;BYTE LENGTH
        LLEF    0,TRAN          ;TRANSLATE TABLE ADDR
        LWLDA   2,.USER,3       ;DESTINATION
        LLEFB   3,BUF2*2        ;SOURCE
        WCMT
; SUBPROGRAM IS DONE, RETURN TO CALLING PROGRAM
DONE:   LDAFP	3
        WRTN

        .END
