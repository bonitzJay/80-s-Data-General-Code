.TITLE	GET
	.ENT	GET

; THIS IS AN ASSEMBLY LANGUAGE SUBPROGRAM CALLABLE FROM
;  A COBOL PROGRAM

;-------- ANDERSON -----------
;	REQUEST = 9(4) COMP
;	AC0     = S9(9) COMP
;	AC1     = S9(9) COMP
;	ARGNUMBER = 9(4) COMP
;	SWITCH    = X(32)
;	RECEIVE-BUFF = X(200)
;	ERROR-WORD 	= 9(9) COMP

;  CALL "GET" USING REQUEST AC0 AC1 ARGNUMBER SWITCH RECEIVE-BUFF
;	      ERROR-WORD.


	AC0=0
	AC1=1
	AC2=2
	AC3=3

	.NREL	0		; UNSHARED CODE

; PARAMETER TABLE

RQ = -12.

A0 = -14.		;BECOMES BYTE ADDRESS OF AC0

A1 = -16.		;BECOMES BYTE ADDRESS OF AC1

AR = -18.		; BECOMES BYTE ADDRESS OF ARGUMENT NUMBER

SW = -20.		;BECOMES BYTE ADDRESS OF SWITCH

BU = -22.		; BECOMES BYTE ADDRESS OF RECEIVE BUFFER

ER = -24.		; BECOMES BYTE ADDRESS OF ERROR ESTATUS WORD


SWN:	.BLK	16.
RBU:	.BLK	100.
;RECEIVE BUFFER
	.ENABLE WORD	;MOST PARAMETERS WORD WIDE
GPACK:	.BLK	?GTLN	;LENGTH OF PACKET
	.LOC	GPACK+?GREQ
	0		;REQUEST
	.LOC	GPACK+?GNUM
	0		;ARGUMENT NUMBER
	.LOC	GPACK+?GSW
	.DWORD 0		;SWITCH
	.LOC	GPACK+?GRES
	.DWORD 0		;RECEIVE BUFFER
	.LOC	GPACK+?GTLN
;END OF PACKET
	.NREL	1		; SHARED CODE

GET:	WSAVS	0		;SAVE ALL
      	LLEF	AC0,TRAN	;TRANSLATE TABLE
	NLDAI	32.,AC1		;LENGTH OF SWITCH
	LLEFB	AC2,SWN*2	;DESTINATION
	LWLDA	AC3,SW,AC3	;SOURCE
	WCMT			;MOVE IT IN
	WSUB	AC1,AC1		;MAKE A NULL
	WSTB	AC2,AC1		;PUT AT END OF SWITCH NAME
;	GET REQUEST
	LDAFP	AC3		;RESTORE FRAME

	LWLDA	AC2,RQ,AC3	;GET REQUEST BP
	WLSHI	-1,AC2		;ADDRESS
	LNLDA	AC0,0,AC2
	LNSTA  	AC0,GPACK+?GREQ	; ADDRESS OF GTMES PACKET
;	GET ARG
	
	LWLDA	AC2,AR,AC3	;BP TO ARGUMENT NO
	WLSHI	-1,AC2		;ADDRESS
	LNLDA	AC0,0,AC2
	LNSTA	AC0,GPACK+?GNUM	;PUT ARG NUMBER INTO PACKET
;	POINTER TO SWITCH
	
	LLEFB	AC2,SWN*2	;SWITCH NAME
	LWSTA	AC2,GPACK+?GSW	;PUT IN PACKET
;POINTER TO RECEIVE BUFFER

	LLEFB	AC2,RBU*2	;BUFFER
	LWSTA	AC2,GPACK+?GRES	;PUT IN PACKET
	
	?GTMES  GPACK
	WBR	ERETN		;ERROR
	;MOVE THE GET BACK TO COBOL
;RETURN AC0 AND AC1
	LWLDA	AC2,A0,AC3	;GET BP TO AC0
	WLSHI	-1,AC2		;ADDRESS
	LWSTA	AC0,0,AC2	;GIVE TO COBOL
	LWLDA	AC2,A1,AC3	;GET B

	WLSHI	-1,AC2		;ADDRESS
	LWSTA	AC1,0,AC2	;HERE COBOL
	WLDAI	200.,AC1
	WLDAI	200.,AC0
	LWLDA	AC2,BU,AC3	;GET BP TO DESTINATION
	LLEFB	AC3,RBU*2	;SOURCE
	WCMV			;MOVE IT MOVE IT.
	WBR	DONE		;BYE

DONE:	WSUB	AC0,AC0		; SET ERROR CODE TO ZERO.
ERETN:	LDAFP	AC3
	LWLDA	AC2,ER,AC3	;GET BP TO ERROR
	WLSHI	-1,AC2		;ADDRESS
	LWSTA	AC0,0,AC2	; MOVE ERROR CODE INTO <ERROR_WORD>.
	WRTN       		; RETURN TO COBOL
        .RDX	2.
TRAN:	;ON BITS OK EXCEPT NULL AND SPACE
	1000000000000000
	0000000000000000
	1000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	0000000000000000
	.RDX	8.
	.END
