.TITLE	GTLEN
	.ENT	GTLEN
; THIS COBOL CALLABLE SUBPROGRAM RETURNS THE
; NUMBER OF LEADING SIGNIFICANT CHARACTERS (INCLUDING
;  EMBEDDED SPACES) IN A FIELD, I.E. FIELD LENGTH
;  MINUS TRAILING SPACES.
;
;  CALLING SEQUENCE :
;	CALL GTLEN USING GKEY,KLENGTH,GTLENGTH
;	WHERE GKEY IS THE FIELD TO BE EXAMINED
;	KLENGTH IS THE MAXIMUM LENGTH OF THAT FIELD
;	GTLENGTH (RETURNED) IS THE FIELD LENGTH MINUS TRAILING SPACES
;	KLENGTH AND GTLENGTH MUST BE PIC 9(9) COMP.
;


	.NREL	0		; UNSHARED CODE

; PARAMETER TABLE

.KEY = -12.		;BYTE POINTER TO FIELD
.LKEY = -14.		;BYTE PTR TO LENGTH OF KEY
.GKEY = -16.		;BYTE PTR TO RESULT



	.NREL	1	; SHARED CODE
	.ENABLE  WORD

;	16 WORD TRANSLATION TABLE FOR CMT INSTRUCTION

;	BIT FOR SPACE IS OFF, ALL OTHERS ON
TRAN:	-1
	-1
	077777
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1


; SUBPROGRAM STARTS HERE
GTLEN:	WSAVR	0    		;SAVE ALL

; INITIALIZATION OF PARAMETER LIST
	LWLDA	2,.LKEY,3	;LENGTH OF KEY BPTR
	WLSHI	-1,2		;ADDR
	LWLDA	1,0,2		;LENGTH
	LWLDA	2,.KEY,3	;BPTR TO FIELD
	WADD	1,2		;BPTR TO END OF FIELD +1
	WSBI	1,2		;BPTR TO END OF FIELD
	WMOV	2,3		;SO IS AC3
	WNEG	1,1		;AC1 IS -LENGTH
	LLEF	0,TRAN		;TRANSLATE TABLE WORD ADDR
	WCMT			;DO IT
	LDAFP	3		;RESTORE FRAME POINTER
	LWLDA	2,.GKEY,3	;WHERE TO PUT RESULTS
	WLSHI	-1,2		;ADDRESS
	WNEG	1,1
	LWSTA	1,0,2		;TELL COBOL
; RETURN TO CALLING PROGRAM
	WRTN

	.END
